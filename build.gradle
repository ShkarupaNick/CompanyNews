apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java-library-distribution'

group = 'io.elastic'
version = '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    mavenCentral()
    mavenLocal()
}
configurations {
    jaxws
}

dependencies {
    compile "io.elastic:sailor-jvm:2.0.1"
    compile "org.glassfish.jersey.core:jersey-client:2.25.1"
    compile "org.glassfish.jersey.media:jersey-media-json-processing:2.25.1"
    jaxws 'com.sun.xml.ws:jaxws-tools:2.1.4'
    compile 'com.sun.xml.bind:jaxb-impl:2.2.8-b01'
}



uploadArchives {
    repositories {
        mavenLocal()
    }
}

task wsimport {
    ext.destDir = file("${projectDir}/src/main/java")
    doLast {
        ant {
            sourceSets.main.output.classesDir.mkdirs()
            destDir.mkdirs()
            taskdef(name: 'wsimport',
                    classname: 'com.sun.tools.ws.ant.WsImport',
                    classpath: configurations.jaxws.asPath
            )
            wsimport(keep: true,
                    destdir: sourceSets.main.output.classesDir,
                    sourcedestdir: destDir,
                    extension: "true",
                    verbose: "false",
                    quiet: "false",
                    package: "io.elastic.dnb.jaxws",
                    xnocompile: "true",
                    wsdl: 'src/main/resources/V3.0/NewsAndMediaProductService.wsdl') {
                xjcarg(value: "-XautoNameResolution")
            }
        }
    }
}

compileJava {
    dependsOn wsimport
    source wsimport.destDir
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}
